import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

sourceCompatibility = 1.8

allprojects {
    group 'org.citygml4j'
    version '2.10.1'
    description 'The Open Source Java API for CityGML'

    repositories {
        jcenter()
    }
}

ext {
    genSrcDir = 'src-gen/main/java'
    date = new Date()
}

sourceSets {
    main.java.srcDir genSrcDir
}

configurations {
    jaxb
}

dependencies {
    api 'org.citygml4j:citygson:1.1.0-b20190623.2134'
    api 'org.glassfish.jaxb:jaxb-runtime:2.3.2'
    api 'org.glassfish.jaxb:xsom:2.3.2'

    jaxb 'org.glassfish.jaxb:jaxb-xjc:2.3.2'
    jaxb 'org.jvnet.jaxb2_commons:jaxb2-basics:0.12.0'
    jaxb 'org.slf4j:slf4j-simple:1.7.25'
}

task generateJaxb(group: 'jaxb') {
    outputs.dir genSrcDir

    doLast {
        mkdir genSrcDir
        
        System.setProperty('javax.xml.accessExternalSchema', 'file')
        ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJC2Task', classpath: configurations.jaxb.asPath)
        ant.xjc(destdir: genSrcDir,
                schema: 'resources/jaxb/citygml4j.xsd',
                binding: 'resources/jaxb/binding.xjb',
                extension: 'true',
                encoding: 'UTF-8') {
            produces(dir: genSrcDir, includes: '**/*.java')
            arg(line: '-Xsetters -Xsetters-mode=direct')
        }

        delete "$genSrcDir/org/citygml4j"
    }
}

task cleanJaxb(type: Delete, group: 'jaxb') {
    delete 'src-gen'
}

javadoc {
    include "org/citygml4j/**"
    options {
        overview "$buildDir/tmp/javadoc/overview.html"
        header "citygml4j $project.version"
        bottom """
            <a target="_blank" href="https://github.com/citygml4j/citygml4j/issues">Report a bug or suggest an enhancement</a><br>
            citygml4j is open source and licensed under the <a target="_blank" href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a><br>
            Official website and more information at: <a target="_blank" href="https://github.com/citygml4j">https://github.com/citygml4j</a><br>
            Copyright &copy; 2013-${date.format('yyyy')}<br>
            Claus Nagel &lt;claus.nagel@gmail.com&gt;
            """.replaceAll("[\r|\n]+", "")
        addStringOption('doctitle', project.name + ' - ' + project.description)
    }
    
    doFirst {
        copy {
            from 'resources/javadoc/overview.html'
            into "$buildDir/tmp/javadoc"
            filter(ReplaceTokens, tokens: [
                    version: project.version
            ])
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main.contents {
    from('resources/doc/README') {
        filter(ReplaceTokens, tokens: [
                version: project.version,
                date: date.format('yyyy-MM-dd'),
                copyright: date.format('yyyy'),
                javaCompatibility: sourceCompatibility.toString(),
                jar: jar.archiveFileName.get(),
                groupId: project.group,
                artifactId: project.name
        ])
    }
    into('lib') {
        from jar
        from configurations.compileClasspath
    }
    into('javadoc') {
        from javadoc
    }
    into('license') {
        from 'LICENSE'
    }
    into('samples') {
        from tasks.getByPath(':citygml4j-samples:copySamples').outputs
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/citygml4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/citygml4j/citygml4j'
                    connection = 'scm:git:https://github.com/citygml4j/citygml4j.git'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/citygml4j'
        vcsUrl = 'https://github.com/citygml4j/citygml4j.git'
        issueTrackerUrl = 'https://github.com/citygml4j/citygml4j/issues'
        labels = ['citygml', 'java', 'ade', 'gis', 'ogc', 'parsing', 'writing']
        licenses = ['Apache-2.0']
        version {
            name = project.version
        }
    }
}