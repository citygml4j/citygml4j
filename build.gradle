import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

allprojects {
    group 'org.citygml4j'
    version '3.0.0-b20200712-1800'
    description 'The Open Source Java API for CityGML'
    ext.date = new Date()

    repositories {
        jcenter()
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    api 'org.xmlobjects.gml:gml-objects:0.9.2-b20200712-1149'
    api 'org.xmlobjects.xal:xal-objects:0.9.2-b20200712-1149'
    annotationProcessor 'org.xmlobjects.gml:gml-objects:0.9.2-b20200712-1149'
}

javadoc {
    options {
        overview "$buildDir/tmp/javadoc/overview.html"
        header "citygml4j $project.version"
        bottom """
            <a target="_blank" href="https://github.com/citygml4j/citygml4j/issues">Report a bug or suggest an enhancement</a><br />
            citygml4j is open source and licensed under the <a target="_blank" href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a><br />
            Official website and more information at: <a target="_blank" href="https://github.com/citygml4j">https://github.com/citygml4j</a><br />
            Copyright &copy; 2013-${date.format('yyyy')}<br />
            Claus Nagel &lt;claus.nagel@gmail.com&gt;
            """.replaceAll("[\r|\n]+", "")
        addStringOption('doctitle', project.name + ' - ' + project.description)
    }

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }

    doFirst {
        copy {
            from 'resources/javadoc/overview.html'
            into "$buildDir/tmp/javadoc"
            filter(ReplaceTokens, tokens: [
                    version: project.version
            ])
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Automatic-Module-Name': project.group
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main.contents {
    from('resources/readme/README') {
        filter(ReplaceTokens, tokens: [
                version: project.version,
                date: date.format('yyyy-MM-dd'),
                copyright: date.format('yyyy'),
                javaCompatibility: java.sourceCompatibility.toString(),
                jar: jar.archiveFileName.get(),
                groupId: project.group,
                artifactId: project.name
        ])
    }
    into('lib') {
        from jar
        from configurations.compileClasspath
    }
    into('javadoc') {
        from javadoc
    }
    into('license') {
        from 'LICENSE'
    }
    into('samples') {
        from tasks.getByPath(':citygml4j-samples:copySamples').outputs
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/citygml4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/citygml4j/citygml4j'
                    connection = 'scm:git:https://github.com/citygml4j/citygml4j.git'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/citygml4j'
        vcsUrl = 'https://github.com/citygml4j/citygml4j.git'
        issueTrackerUrl = 'https://github.com/citygml4j/citygml4j/issues'
        labels = ['citygml', 'java', 'ade', 'gis', 'ogc', 'parsing', 'writing']
        licenses = ['Apache-2.0']
        version {
            name = project.version
        }
    }
}