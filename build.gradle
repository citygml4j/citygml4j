import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'distribution'
    id 'maven-publish'
    id 'signing'
    id 'idea'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

allprojects {
    group 'org.citygml4j'
    version '3.0.0-SNAPSHOT'
    description 'The Open Source Java API for CityGML'
    ext.date = new Date()

    repositories {
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        mavenCentral()
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.named('compileJava') {
    options.javaModuleVersion = provider { project.version }
}

dependencies {
    api 'org.xmlobjects.gml:gml-objects:1.0.0-SNAPSHOT'
    api 'org.xmlobjects.xal:xal-objects:1.0.0-SNAPSHOT'
    annotationProcessor 'org.xmlobjects.gml:gml-objects:1.0.0-SNAPSHOT'
}

idea {
    module {
        outputDir file('out/production/classes')
    }
}

javadoc {
    options {
        overview "$buildDir/tmp/javadoc/overview.html"
        header "citygml4j $project.version"
        bottom """
            <a target="_blank" href="https://github.com/citygml4j/citygml4j/issues">Report a bug or suggest an enhancement</a><br />
            citygml4j is open source and licensed under the <a target="_blank" href="http://www.apache.org/licenses/LICENSE-2.0">Apache License, Version 2.0</a><br />
            Official website and more information at: <a target="_blank" href="https://github.com/citygml4j">https://github.com/citygml4j</a><br />
            Copyright &copy; 2013-${date.format('yyyy')}<br />
            Claus Nagel &lt;claus.nagel@gmail.com&gt;
            """.replaceAll("[\r|\n]+", "")
        addStringOption('doctitle', project.name + ' - ' + project.description)
    }

    doFirst {
        copy {
            from 'resources/javadoc/overview.html'
            into "$buildDir/tmp/javadoc"
            filter(ReplaceTokens, tokens: [
                    version: project.version
            ])
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main.contents {
    from('resources/readme/README') {
        filter(ReplaceTokens, tokens: [
                version: project.version,
                date: date.format('yyyy-MM-dd'),
                copyright: date.format('yyyy'),
                javaCompatibility: java.sourceCompatibility.toString(),
                jar: jar.archiveFileName.get(),
                groupId: project.group,
                artifactId: project.name
        ])
    }
    into('lib') {
        from jar
        from configurations.compileClasspath
    }
    into('javadoc') {
        from javadoc
    }
    into('license') {
        from 'LICENSE'
    }
    into('samples') {
        from tasks.getByPath(':citygml4j-samples:copySamples').outputs
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/citygml4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/citygml4j/citygml4j'
                    connection = 'scm:git:https://github.com/citygml4j/citygml4j.git'
                }
            }
        }
    }
}

if (!project.hasProperty("skip.signing")) {
    signing {
        sign publishing.publications.mavenJava
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}